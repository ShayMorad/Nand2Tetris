// This file is part of nand2tetris, as taught in The Hebrew University, and
// was written by Aviv Yaish. It is an extension to the specifications given
// [here](https://www.nand2tetris.org) (Shimon Schocken and Noam Nisan, 2017),
// as allowed by the Creative Common Attribution-NonCommercial-ShareAlike 3.0
// Unported [License](https://creativecommons.org/licenses/by-nc-sa/3.0/).
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    //zx
    Mux16(a=x ,b=false ,sel=zx ,out=zerox);

    //nx
    Not16(in=zerox ,out=notzx);
    Mux16(a=zerox, b=notzx, sel=nx, out=zxnx);

    //zy
    Mux16(a=y ,b=false ,sel=zy ,out=zeroy);

    //ny
    Not16(in=zeroy ,out=notzy);
    Mux16(a=zeroy, b=notzy, sel=ny, out=zyny);

    //f
    Add16(a=zxnx ,b=zyny ,out=xPLUSy);
    And16(a=zxnx ,b=zyny ,out=xANDy);
    Mux16(a=xANDy, b=xPLUSy, sel=f, out=xfy);

    //no
    Not16(in=xfy ,out=notf);
    Mux16(a=xfy, b=notf, sel=no, out=out, out[0..7]=or1, out[8..15]=or2, out[15]=ng);

    //zr
    Or8Way(in=or1 ,out=or11);
    Or8Way(in=or2 ,out=or22);
    Or(a=or11, b=or22, out=ifzr);
    Mux(a=true, b=false, sel=ifzr, out=zr);
}